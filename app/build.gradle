buildscript {
  repositories {
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath 'io.fabric.tools:gradle:1.+'
  }
}
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'
apply plugin: 'android-apt'

repositories {
  maven { url 'https://maven.fabric.io/public' }
}


android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"

  defaultConfig {
    applicationId "com.bowyer.app.playermanage"
    minSdkVersion 16
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
  }

  lintOptions {
    checkReleaseBuilds false;
  }

  signingConfigs {
    develop {
      storeFile file('../develop.keystore')
      storePassword System.getenv('DVELOP_STORE_PASSWORD')
      keyAlias System.getenv('DEVELOP_KEY_ALIAS')
      keyPassword System.getenv('DEVELOP_KEY_PASSWORD')
    }
    release {
      storeFile file('../release.keystore')
      storePassword System.getenv('STORE_PASSWORD')
      keyAlias System.getenv('KEY_ALIAS')
      keyPassword System.getenv('KEY_PASSWORD')
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix ".debug"
      ext.enableCrashlytics = false
      buildConfigField("String", "GA_TRACKING_ID", "\"UA-77698878-2\"")
    }

    develop {
      applicationIdSuffix ".develop"
      zipAlignEnabled true
      debuggable true
      signingConfig signingConfigs.develop
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
      def releaseNotes = 'git log --pretty=format:"%s" --since "yesterday"'.execute([],
          project.rootDir).text.trim()
      ext {
        betaDistributionGroupAliases = "beta_tester"
        betaDistributionReleaseNotes = releaseNotes
      }
      buildConfigField("String", "GA_TRACKING_ID", "\"UA-77698878-2\"")
    }

    release {
      minifyEnabled false
      zipAlignEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
      signingConfig signingConfigs.release
      buildConfigField("String", "GA_TRACKING_ID", "\"UA-77698878-1\"")
    }
  }

  compileOptions {
    encoding = 'UTF-8'
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
    exclude 'META-INF/maven/com.google.auto/auto-common/pom.xml'
    exclude 'META-INF/maven/com.google.auto/auto-common/pom.properties'
    exclude 'META-INF/maven/com.google.auto.service/auto-service/pom.xml'
    exclude 'META-INF/maven/com.google.auto.service/auto-service/pom.properties'
    exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
    exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
  }
}

dependencies {
  // Google Support
  compile 'com.android.support:appcompat-v7:23.3.0'
  compile 'com.android.support:support-v4:23.1.1'
  compile 'com.android.support:recyclerview-v7:23.1.1'
  compile 'com.android.support:design:23.1.1'

  // Google
  compile 'com.google.android.gms:play-services-base:8.4.0'
  compile 'com.google.android.gms:play-services-analytics:8.4.0'
  compile 'com.google.android.gms:play-services-gcm:8.4.0'
  compile 'com.google.android.gms:play-services-plus:8.4.0'
  compile 'com.growthbeat:growthbeat-android:1.2.6@aar'

  //DI
  compile 'com.google.dagger:dagger:2.2'
  apt 'com.google.dagger:dagger-compiler:2.2'
  provided 'javax.annotation:jsr250-api:1.0'

  //Injection
  compile 'com.jakewharton:butterknife:7.0.1'

  // UI
  compile 'com.github.ksoichiro:android-observablescrollview:1.5.0'
  compile 'com.github.aakira:expandable-layout:1.5.1@aar'

  //Log
  compile 'com.jakewharton.timber:timber:2.7.1'

  //DB
  compile 'com.squareup.sqlbrite:sqlbrite:0.6.3'

  //Parcel
  compile 'com.google.auto.value:auto-value:1.2-rc1'
  compile 'com.ryanharter.auto.value:auto-value-parcel:0.2.0'

  //Rx
  compile 'io.reactivex:rxjava:1.1.5'
  compile 'io.reactivex:rxandroid:1.2.0'
  compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'

  //License
  compile 'net.yslibrary.licenseadapter:licenseadapter:1.2.1'

  //Lambda
  retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

  compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
    transitive = true;
  }
}
