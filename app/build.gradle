apply plugin: 'com.android.application'

android {
  compileSdkVersion 30

  defaultConfig {
    applicationId "com.bowyer.app.playermanage"
    minSdkVersion 21
    targetSdkVersion 30
    versionCode 3
    versionName "1.0.1"
    multiDexEnabled true
    javaCompileOptions {
      annotationProcessorOptions {
        includeCompileClasspath false
      }
    }
  }

  dexOptions {
    incremental true
    javaMaxHeapSize "4g"
  }

  dataBinding {
    enabled = true
  }

  lintOptions {
    checkReleaseBuilds false;
  }

  signingConfigs {
    develop {
      storeFile file('../develop.keystore')
      storePassword System.getenv('DVELOP_STORE_PASSWORD')
      keyAlias System.getenv('DEVELOP_KEY_ALIAS')
      keyPassword System.getenv('DEVELOP_KEY_PASSWORD')
    }
    release {
      storeFile file('../release.keystore')
      storePassword System.getenv('STORE_PASSWORD')
      keyAlias System.getenv('KEY_ALIAS')
      keyPassword System.getenv('KEY_PASSWORD')
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix ".debug"
      ext.enableCrashlytics = false
      buildConfigField("String", "GA_TRACKING_ID", "\"UA-77698878-2\"")
    }

    develop {
      applicationIdSuffix ".develop"
      zipAlignEnabled true
      debuggable true
      signingConfig signingConfigs.develop
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
      def releaseNotes = 'git log --pretty=format:"%s" --since "yesterday"'.execute([],
          project.rootDir).text.trim()
      ext {
        betaDistributionGroupAliases = "beta_tester"
        betaDistributionReleaseNotes = releaseNotes
      }
    }

    release {
      minifyEnabled false
      zipAlignEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
      signingConfig signingConfigs.release
      buildConfigField("String", "GA_TRACKING_ID", "\"UA-77698878-1\"")
    }
  }

  compileOptions {
    encoding = 'UTF-8'
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
    exclude 'META-INF/maven/com.google.auto/auto-common/pom.xml'
    exclude 'META-INF/maven/com.google.auto/auto-common/pom.properties'
    exclude 'META-INF/maven/com.google.auto.service/auto-service/pom.xml'
    exclude 'META-INF/maven/com.google.auto.service/auto-service/pom.properties'
    exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
    exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
  }
}

dependencies {
  // Google Support
  implementation 'androidx.appcompat:appcompat:1.2.0'
  implementation 'androidx.legacy:legacy-support-v4:1.0.0'
  implementation 'androidx.recyclerview:recyclerview:1.2.0'
  implementation 'com.google.android.material:material:1.3.0'

  // Google
  implementation 'com.google.android.gms:play-services-base:8.4.0'
  implementation 'com.google.android.gms:play-services-gcm:15.0.1'
  implementation 'com.google.android.gms:play-services-plus:8.4.0'

  // Push
  implementation 'com.growthbeat:growthbeat-android:1.2.6@aar'

  //
  implementation 'com.squareup:javapoet:1.7.0'

  // DI
  implementation 'com.google.dagger:dagger:2.6'
  annotationProcessor 'com.google.dagger:dagger-compiler:2.6'
  compileOnly 'javax.annotation:jsr250-api:1.0'

  // UI
  implementation 'com.github.ksoichiro:android-observablescrollview:1.5.0'
  implementation 'com.github.aakira:expandable-layout:1.5.1@aar'

  // Log
  implementation 'com.jakewharton.timber:timber:2.7.1'

  // DB
  implementation 'com.squareup.sqlbrite:sqlbrite:0.6.3'

  // Parcel
  implementation 'com.google.auto.value:auto-value:1.3-rc1'
  annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.3-rc2'
  annotationProcessor 'com.gabrielittner.auto.value:auto-value-cursor:1.0.0-rc1'
  implementation 'com.gabrielittner.auto.value:auto-value-cursor-annotations:1.0.0-rc1'

  // Rx
  implementation 'io.reactivex:rxjava:1.1.5'
  implementation 'io.reactivex:rxandroid:1.2.0'
  implementation 'com.jakewharton.rxbinding:rxbinding:0.4.0'

  // License
  implementation 'net.yslibrary.licenseadapter:licenseadapter:1.2.1'
}
